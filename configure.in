# Configure.in
#
#  This file tests for various compiler features needed to configure
#  the gtkmm package.  Original skeleton was provided by Stephan Kulow.
#  All tests were written by Tero Pulkkinen, Mirko Streckenbach, and
#  Karl Nelson.
#
#
#  Copyright 2001 Free Software Foundation
#  Copyright 1999 gtkmm Development Team
#  Copyright 1998 Stephan Kulow
#
AC_INIT(goocanvas/goocanvasmmconfig.h.in)

#########################################################################
#  Version and initialization
#########################################################################
LIBGOOCANVASMM_MAJOR_VERSION=0
LIBGOOCANVASMM_MINOR_VERSION=3
LIBGOOCANVASMM_MICRO_VERSION=0

#
# +1 : ? : +1  == new interface that does not break old one
# +1 : ? : 0   == new interface that breaks old one
#  ? : ? : 0   == no new interfaces, but breaks apps
#  ? :+1 : ?   == just some internal changes, nothing breaks but might work
#                 better
# CURRENT : REVISION : AGE
LIBGOOCANVASMM_SO_VERSION=1:0:0

LIBGOOCANVASMM_VERSION=$LIBGOOCANVASMM_MAJOR_VERSION.$LIBGOOCANVASMM_MINOR_VERSION.$LIBGOOCANVASMM_MICRO_VERSION
LIBGOOCANVASMM_RELEASE=$LIBGOOCANVASMM_MAJOR_VERSION.$LIBGOOCANVASMM_MINOR_VERSION
AC_DEFINE_UNQUOTED(LIBGOOCANVASMM_MAJOR_VERSION, $LIBGOOCANVASMM_MAJOR_VERSION)
AC_DEFINE_UNQUOTED(LIBGOOCANVASMM_MINOR_VERSION, $LIBGOOCANVASMM_MINOR_VERSION)
AC_DEFINE_UNQUOTED(LIBGOOCANVASMM_MICRO_VERSION, $LIBGOOCANVASMM_MICRO_VERSION)
AC_DEFINE_UNQUOTED(LIBGOOCANVASMM_VERSION, "$LIBGOOCANVASMM_VERSION")
AC_SUBST(LIBGOOCANVASMM_VERSION)
AC_SUBST(LIBGOOCANVASMM_RELEASE)
AC_SUBST(LIBGOOCANVASMM_SO_VERSION)

AC_CONFIG_AUX_DIR(scripts)

dnl For automake.
VERSION=$LIBGOOCANVASMM_VERSION
PACKAGE=goocanvasmm

dnl Initialize automake stuff
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

dnl Specify a configuration file (no autoheader)
AM_CONFIG_HEADER(goocanvas/goocanvasmmconfig.h)
AM_MAINTAINER_MODE
AL_ACLOCAL_INCLUDE(scripts)


#########################################################################
#  Configure arguments
#########################################################################

#########################################################################
#  Environment Checks
#########################################################################
AC_PROG_CC
AC_PROG_CPP
AC_PROG_MAKE_SET
AC_CANONICAL_HOST
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

AL_PROG_GNU_M4(AC_MSG_ERROR([dnl
SUN m4 does not work for building gtkmm.
Please install GNU m4.]))

AL_PROG_GNU_MAKE(AC_MSG_ERROR([dnl
SUN make does not work for building gtkmm.
Please install GNU make.]))

AL_PROG_PERL(AC_MSG_ERROR([Perl is required to build gtkmm.]))

AC_CHECK_PROGS(PERL, perl5 perl)

#########################################################################
#  Dependancy checks
#########################################################################
PKG_CHECK_MODULES(LIBGOOCANVASMM, gtkmm-2.4 >= 2.10.9 cairomm-1.0 >= 1.2.2 glibmm-2.4 >= 2.10.0 goocanvas >= 0.8 )
AC_SUBST(LIBGOOCANVASMM_CFLAGS)
AC_SUBST(LIBGOOCANVASMM_LIBS)

GLIBMM_PREFIX=`pkg-config --variable=libdir glibmm-2.4`
GMM_PROCDIR=$GLIBMM_PREFIX/glibmm-2.4/proc
AC_SUBST(GMM_PROCDIR)
GMM_PROC=$GMM_PROCDIR/gmmproc
AC_SUBST(GMM_PROC)

#########################################################################
#  C++ checks
#########################################################################
AC_PROG_CXX
AC_LANG_CPLUSPLUS


# Create a list of input directories for Doxygen.  We have to check wether
# we are building in the source directory, because Doxygen doesn't like
# duplicated directories in the input list.
LIBGOOCANVASMM_DOXYGEN_INPUT=''
gtkmm_srcdir=`cd "$srcdir" && pwd`
gtkmm_builddir=`pwd`
for sublib in goocanvas
do
  LIBGOOCANVASMM_DOXYGEN_INPUT="$LIBGOOCANVASMM_DOXYGEN_INPUT $gtkmm_builddir/$sublib/$sublib""mm/"
  test "x$gtkmm_builddir" = "x$gtkmm_srcdir" || \
    LIBGOOCANVASMM_DOXYGEN_INPUT="$LIBGOOCANVASMM_DOXYGEN_INPUT $gtkmm_srcdir/$sublib/$sublib""mm/"
done
AC_SUBST(LIBGOOCANVASMM_DOXYGEN_INPUT)



# Dummy conditional just to make automake-1.4 happy.
# We need an always-false condition in docs/Makefile.am.
AM_CONDITIONAL(LIBGOOCANVASMM_FALSE,[false])

# HACK:  Assign a dummy in order to prevent execution of autoheader by the
# maintainer-mode rules.  That would fail since we aren't using autoheader.
AUTOHEADER=':'


AC_OUTPUT([
  Makefile

  goocanvas/Makefile
  goocanvas/goocanvasmm-1.0.pc
  goocanvas/src/Makefile
  goocanvas/goocanvasmm/Makefile
  goocanvas/goocanvasmm/private/Makefile

  tools/Makefile
    tools/extra_defs_gen/Makefile
    tools/m4/Makefile

  scripts/Makefile

  docs/Makefile
  docs/reference/Makefile
    docs/reference/Doxyfile

  examples/Makefile
    examples/simple/Makefile
    examples/moving_shapes/Makefile
    examples/demo/Makefile
    examples/coordinates/Makefile
    examples/text/Makefile
])
:
